REM by https://github.com/biletnikov/avr-nec-ir-decoder/tree/master/Proteus
REM Enter the generator script here.

FLOAT DELAY=1.0
FLOAT START_MARKER=0.009
FLOAT PULSE=0.000560
FLOAT PAUSE_0=0.000560
FLOAT PAUSE_1=0.001690
FLOAT REPEAT_PULSE_DELAY=0.00225
REM address + inversed address
INT ADDRESS=0b01100110
INT ADDRESS_INV=0b10011001
REM command + inversed command
INT COMMAND=0b01100011
INT COMMAND_INV=0b10011100

REM number of repeats
INT REPEAT_COUNT=3

INT j = 0

INT SEND_DATA=COMMAND_INV
SEND_DATA=SEND_DATA<<8
SEND_DATA=SEND_DATA+COMMAND
SEND_DATA=SEND_DATA<<8
SEND_DATA=SEND_DATA+ADDRESS_INV
SEND_DATA=SEND_DATA<<8
SEND_DATA=SEND_DATA+ADDRESS


OUT=1
SLEEP FOR DELAY

REM start new packet
OUT=0
SLEEP FOR START_MARKER
OUT=1
SLEEP FOR 0.0045

REM SEND 4 bytes data
FOR j=0 TO 31
  OUT=0
  SLEEP FOR PULSE
  OUT=1
 IF (SEND_DATA&(1<<j)) == 0
   SLEEP FOR PAUSE_0
   ELSE
   SLEEP FOR PAUSE_1
 ENDIF
NEXT j
REM last pulse
OUT=0
SLEEP FOR PULSE
OUT=1



REM check do we need to repeat the message
IF REPEAT_COUNT > 0

 FOR j=0 TO REPEAT_COUNT-1

  REM wait for 40 ms before repeat
  OUT = 1
  SLEEP FOR 0.040
  
  OUT=0
  SLEEP FOR START_MARKER
  OUT=1
  SLEEP FOR REPEAT_PULSE_DELAY
  OUT=0
  SLEEP FOR PULSE

 NEXT j

ENDIF

OUT = 1
